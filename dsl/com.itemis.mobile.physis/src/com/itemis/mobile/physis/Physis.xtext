// updated for Xtext 2
grammar com.itemis.mobile.physis.Physis with org.eclipse.xtext.common.Terminals

generate physis "http://www.itemis.com/mobile/physis/Physis"

Model:
	(datamodels += DataModel)*;
	
enum Platform:
	iOS | Android;
	
DataModel:
	'datamodel' name=ID '{'
		(entities += Entity)*
	'}';
		
DateType:
	{DateType} "Date";

IntegerType:
	{IntegerType} "Integer";
	
DoubleType:
	{DoubleType} "Double";
	
FloatType:
	{FloatType} "Float";
			
StringType:
	{StringType} "String";
	
BooleanType:
	{BooleanType} "Boolean";
	
BuiltInType:
	DateType | IntegerType | DoubleType | FloatType | StringType | BooleanType;
	
Reference:
	TypeReference | BuiltInType;
	
TypeReference:
	type=[Entity];
	
Entity:
	'entity' name=ID '{'
		(attributes += Attribute)*
	'}';
	
Attribute:
	name=ID ':' type=Reference (multiplicity?='[]')?;