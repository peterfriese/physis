// updated for Xtext 2
grammar com.itemis.mobile.physis.Physis with org.eclipse.xtext.common.Terminals

generate physis "http://www.itemis.com/mobile/physis/Physis"

Model:
	(datamodels += DataModel)*;
	
enum Platform:
	iOS | Android;
	
DataModel:
	'datamodel' name=ID '{'
		(types += Type)*
	'}';
		
Type:
	SimpleType | Entity;
	
MappingClause:
	'is' mappedType=ID 'on platform' platform=Platform;
	
SimpleType:
	'type' name=ID (mappings += MappingClause)*;
	
Entity:
	'entity' name=ID '{'
		(attributes += Attribute)*
	'}';
	
Attribute:
	name=ID ':' type=[Type] (multiplicity?='[]')?;